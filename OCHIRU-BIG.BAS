' O-CHI-RUuuu a.k.a. fall | IchigoJam BIG / IchigoJam BASIC 1.2.2
' Copyright (c) 2015-2018 BALLOON | FU-SEN
' The MIT License (MIT) - https://mit.balloon.net.eu.org/#2015-2018
10 'ｵﾁﾙ~
15 IF VER()>12233 VIDEO 3
20 CLS:X=7:S=0:Y=0
30 FOR Y=0 TO 11:GOSUB 170:NEXT
40 IF VPEEK(X,1) GOTO 130
50 GOSUB 170
60 LOCATE RND(11)+3,11:? "+"
70 LOCATE X,0:? CHR$(250);
80 WAIT 5
90 I=INKEY()
100 IF I=28 LET X,X-1
110 IF I=29 LET X,X+1
120 S=S+1:GOTO 40
130 LOCATE 0,11:?
140 LOCATE X,0:? CHR$(244);
150 LOCATE 0,10:? "SCORE:";S;" ";
160 END
170 LOCATE 2,Y:? CHR$(1);
180 LOCATE 14,Y:? CHR$(1);
190 RETURN

' MAZE | IchigoJam BASIC 0.9.9
' Copyright (c) 2015-2019 Keiichi Shiga (BALLOON | FU-SEN)
' The MIT License (MIT) - https://gist.github.com/fu-sen/c0611b12b5ce41773d48282a21e9c215
10 'ﾒｲﾛ
20 CLS:LET Y,0:LET X,0
30 LC 0,Y:? CHR$(1);
40 LC 30,Y:? CHR$(1);
50 LET Y,Y+1:IF Y<>23 GOTO 30
60 LC X,0:? CHR$(1);
70 LC X,22:? CHR$(1);
80 LET X,X+1:IF X<>30 GOTO 60
90 LET X,2
100 LET Y,2
110 LC X,Y:? CHR$(1);
120 LET K,RND(4)
130 IF K==1 LC X-1,Y:GOTO 170
140 IF K==2 LC X,Y+1:GOTO 170
150 IF K==3 LC X+1,Y:GOTO 170
160 LC X,Y-1
170 ? CHR$(1);
180 LET Y,Y+2:IF Y<=20 GOTO 110
190 LET X,X+2:IF X<=28 GOTO 100
200 LET X,1:LET Y,1
210 LET A,1:LET B,1
220 LC 29,21:? CHR$(2);
230 LC X,Y:? CHR$(249);
240 IF (X==29)*(Y==21) END
250 LET I,INKEY()
260 IF I==28 LET X,X-1
270 IF I==29 LET X,X+1
280 IF I==30 LET Y,Y-1
290 IF I==31 LET Y,Y+1
300 IF (A==X)*(B==Y) GOTO 250
310 IF SCR(X,Y)<>1 GOTO 330
320 LET X,A:LET Y,B:GOTO 250
330 LC A,B:? " ";
340 LET A,X:LET B,Y:GOTO 230

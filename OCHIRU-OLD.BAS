' O-CHI-RUuuu a.k.a. fall | IchigoJam BASIC
' Copyright (c) 2015-2018 BALLOON | FU-SEN
' The MIT License (MIT) - https://mit.balloon.net.eu.org/#2015-2018
10 CLS:LET X,17:LET S,0:LET Y,0
20 GOSUB 160
30 LET Y,Y+1:IF Y<>26 GOTO 20
40 IF VPEEK(X,1)<>0 GOTO 120
50 GOSUB 160
60 LOCATE RND(11)+12,26:? "+"
70 LOCATE X,0:? CHR$(6);
75 ' WAIT 5 ' IchigoJam 0.9.4-
80 LET I,INKEY()
90 IF I==28 LET X,X-1
100 IF I==29 LET X,X+1
110 LET S,S+1:GOTO 40
120 LOCATE 0,26:?
130 LOCATE X,0:? CHR$(15);
140 LOCATE 0,25:? "SCORE:";S;" ";
150 END
160 LOCATE 11,Y:? CHR$(1);
170 LOCATE 23,Y:? CHR$(1);
180 RETURN

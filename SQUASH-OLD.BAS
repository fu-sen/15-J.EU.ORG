' Squash | IchigoJam BASIC
' Copyright (c) 2003-2015 Keiichi Shiga (BALLOON | FU-SEN)
' The MIT License (MIT) - https://mit.balloon.net.eu.org/#2003-2015
10 CLS:LET X,1:LET Y,0
20 LC X,0:? CHR$(1);
30 LET X,X+1:IF X<>35 GOTO 20
40 LC 0,Y:? CHR$(1);
50 LC 35,Y:? CHR$(1);
60 LET Y,Y+1:IF Y<>26 GOTO 40
70 LET S,0:LET X,RND(33)+1
80 LET Y,1:LET N,1:LET P,17
90 LET M,1:IF X<18 LET M,-1
100 LC X,Y:? CHR$(11);:LC P-1,25
110 ? CHR$(P=1);CHR$(1);CHR$(1);
120 ? CHR$(1);CHR$(P=32);
130 IF Y=25 LC 11,26:END
140 IF X=1 LET M,1
150 IF X=34 LET M,-1
160 IF Y=1 LET N,1
170 IF Y<24 GOTO 210
180 IF (X<P-1)+(X>P+3) GOTO 210
190 LET N,-1:LET S,S+1
200 LC 0,26:? "SCORE ";S;
210 LET I,INKEY()
220 IF (P>1)*(I=28) LET P,P-1
230 IF (P<32)*(I=29) LET P,P+1
240 LC X,Y:? " ";
250 LET X,X+M:LET Y,Y+N:GOTO 100

' Squash | IchigoJam BASIC 0.9.9
' Copyright (c) 2003-2015 Keiichi Shiga (BALLOON | FU-SEN)
' The MIT License (MIT) - https://mit.balloon.net.eu.org/#2003-2015
1 'ｲﾜﾕﾙ ｽｶｯｼｭ
10 CLS:LET X,1:LET Y,0
20 LC X,0:? CHR$(1);
30 LET X,X+1:IF X<>31 GOTO 20
40 LC 0,Y:? CHR$(1);
50 LC 31,Y:? CHR$(1);
60 LET Y,Y+1:IF Y<>23 GOTO 40
70 LET S,0:LET X,RND(29)+1
80 LET Y,1:LET N,1:LET P,10
90 LET M,1:IF X<16 LET M,-1
100 LC X,Y:? CHR$(232);
110 LC P-1,22
120 ? CHR$(P=1,1,1,1,P=28);
125 WAIT 5
130 IF Y=22 LC 11,23:END
140 IF X=1 LET M,1
150 IF X=30 LET M,-1
160 IF Y=1 LET N,1
170 IF Y<21 GOTO 210
180 IF (X<P-1)+(X>P+3) GOTO 210
190 LET N,-1:LET S,S+1
200 LC 0,24:? "SCORE ";S;
210 LET I,INKEY()
220 IF (P>1)*(I=28) LET P,P-1
230 IF (P<28)*(I=29) LET P,P+1
240 LC X,Y:? " ";
250 LET X,X+M:LET Y,Y+N:GOTO 100

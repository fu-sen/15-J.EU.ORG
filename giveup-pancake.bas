' GIVE UP! | PanCake / IchigoJam BASIC
' Copyright (c) 1995-2015 Keiichi Shiga (BALLOON | FU-SEN)
' The MIT License (MIT) - https://mit.balloon.net.eu.org/#1995-2015
10 'PanCake GIVE UP!
20 CLV:X=20:Y=22:Q=0:R=1
25 IF VER()>11000 UART 1
30 ? CHR$(128,4,5,1):VIDEO 0
40 ? CHR$(128,4,0,11)
50 W=RND(40):V=RND(45)
60 IF W-X<-2 OR W-X>2 GOTO 90
70 IF V-Y<-2 OR V-Y>2 GOTO 90
80 GOTO 50
90 Z=PEEK(#800+W/8+V*5)
100 POKE #800+W/8+V*5,Z|(1<<(W%8))
110 ? CHR$(128,8,1,W*2,V,W*2+1,V,1)
120 W=(BTN(RIGHT))-(BTN(LEFT))
130 V=(BTN(DOWN))-(BTN(UP))
140 IF W<>0 OR V<>0 Q=W:R=V
150 IF X+Q<0 OR X+Q>39 GOTO 230 
160 IF Y+R<0 OR Y+R>44 GOTO 230
170 X=X+Q:Y=Y+R
180 Z=PEEK(#800+X/8+Y*5)
190 IF Z&(1<<(X%8)) GOTO 230
200 POKE #800+X/8+Y*5,Z|(1<<(X%8))
210 ? CHR$(128,8,1,X*2,Y,X*2+1,Y,1)
220 S=S+1:WAIT 10:GOTO 50
230 FOR T=0 TO 49
240 ? CHR$(128,8,1,X*2,Y);
250 ? CHR$(RND(80),RND(45),RND(16))
260 NEXT
270 IF !INKEY(SPACE) GOTO 270
280 ? CHR$(128,4,5,0):VIDEO 1
290 ?"SCORE:";S

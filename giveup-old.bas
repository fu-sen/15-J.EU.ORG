' GIVE UP! | IchigoJam BASIC
' Copyright (c) 1995-2015 Keiichi Shiga (BALLOON | FU-SEN)
' The MIT License (MIT) - https://gist.github.com/fu-sen/26afd90c4720f227cccb
10 CLS:LET X,0:LET Y,0
20 LOCATE 0,Y:? CHR$(1);
30 LOCATE 35,Y:? CHR$(1);
40 LET Y,Y+1:IF Y<>26 GOTO 20
50 LOCATE X,0:? CHR$(1);
60 LOCATE X,25:? CHR$(1);
70 LET X,X+1:IF X<>36 GOTO 50 
80 LET S,0:LET X,17:LET Y,13
90 LET M,0:LET N,1
100 LOCATE X,Y:? CHR$(2);:LET C,0
110 LET R,RND(33)+1:LET Q,RND(22)+1
120 IF (R>X-3)*(R<X+3) LET C,C+1
130 IF (Q>Y-3)*(Q<Y+3) LET C,C+1
140 IF C==2 GOTO 110
150 LC R,Q:? "+";
160 LET I,INKEY()
170 IF I==28 LET M,-1:LET N,0
180 IF I==29 LET M,1:LET N,0
190 IF I==30 LET M,0:LET N,-1
200 IF I==31 LET M,0:LET N,1
210 LOCATE 0,26:? "SCORE ";S;
220 LET X,X+M:LET Y,Y+N
230 IF VPEEK(X,Y)<>0 GOTO 250
240 LET S,S+1:GOTO 100
250 LOCATE X,Y:? CHR$(15);
260 LOCATE 10,26:END
